# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

copy("copy_nala_color_ids") {
  sources = [ "//brave/node_modules/@brave/leo/tokens/skia/nala_color_id.h" ]

  outputs = [ "$root_out_dir/gen/brave/ui/color/nala/{{source_file_part}}" ]
}

source_set("nala_color_id") {
  sources = get_target_outputs(":copy_nala_color_ids")

  deps = [ ":copy_nala_color_ids" ]
}

copy("copy_nala_color_mixer") {
  sources = [
    "//brave/node_modules/@brave/leo/tokens/skia/nala_color_mixer.cc",
    "//brave/node_modules/@brave/leo/tokens/skia/nala_color_mixer.h",
  ]

  outputs = [ "$root_out_dir/gen/brave/ui/color/nala/{{source_file_part}}" ]
}

source_set("brave_color_mixers") {
  sources = get_target_outputs(":copy_nala_color_mixer") + [
              "missing_color_ref_mixer.cc",
              "missing_color_ref_mixer.h",
            ]

  deps = [
    ":copy_nala_color_mixer",
    ":nala_color_id",
    "//ui/color",
    "//ui/color:color_headers",
    "//ui/color:color_provider_key",
    "//ui/color/dynamic_color",
  ]
}
