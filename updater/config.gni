# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

declare_args() {
  # In 2023, Chrome began switching to a new auto-update implementation
  # ("Omaha 4"). As of this writing, Chrome uses Omaha 4 on macOS and is in the
  # process of rolling it out on Windows. The new implementation is hidden
  # behind upstream GN arg `enable_updater`. We set it to true to enable some
  # integration points. But, as of this writing, we do not yet use Omaha 4. The
  # arg below enables building our own Omaha 4 integration, which is still
  # experimental.
  brave_enable_updater = false
}

declare_args() {
  # It would be nice to declare build_omaha4 in the declare_args block above.
  # But its default value depends on brave_enable_updater. In this case, GN
  # requires us to create a separate declare_args block.
  build_omaha4 = brave_enable_updater && !is_official_build
}

if (brave_enable_updater) {
  assert(target_os == "mac",
         "brave_enable_updater currently only takes effect on macOS.")
}

if (build_omaha4) {
  assert(brave_enable_updater,
         "brave_enable_updater must be true to build Omaha 4")
}
